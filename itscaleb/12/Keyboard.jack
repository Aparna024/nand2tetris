// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

	// A pointer to the keyboard io memory mapped address
	static int KeyboardMemAddr;
	static int MaxStringLength;

    /** Initializes the keyboard. */
    function void init() {
		let KeyboardMemAddr = 24576;
		let MaxStringLength = 50;
		return;
    } 

    /**
     * Returns the ASCII code (as char) of the currently pressed key,
     * or 0 if no key is currently pressed.
     * Recognizes all ASCII characters, as well as the following extension
     * of action keys:
     * New line = 128 = String.newLine()
     * Backspace = 129 = String.backSpace()
     * Left Arrow = 130
     * Up Arrow = 131
     * Right Arrow = 132
     * Down Arrow = 133
     * Home = 134
     * End = 135
     * Page Up = 136
     * Page Down = 137
     * Insert = 138
     * Delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
		return Memory.peek(KeyboardMemAddr);
    }

    /**								
     * Reads the next character from the keyboard.
     * waits until a key is pressed and then released, then echoes
     * the key to the screen, and returns the value of the pressed key.
     */
    function char readChar() {
		var char pressedKey, value;

		while(pressedKey = 0)
		{
			let pressedKey = Memory.peek(KeyboardMemAddr);
		}
		let value = pressedKey;

		while(~(pressedKey = 0))
		{
			let pressedKey = Memory.peek(KeyboardMemAddr);
		}

		do Output.printChar(value);
		return value;
    }

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its value.
     */
    function String readLine(String message) {
		var String inputString;
		var char pressedKey;

		let inputString = String.new(MaxStringLength);
		let pressedKey = 0;

		do Output.printString(message);

		while(~(pressedKey = String.newLine()))
		{
			let pressedKey = Keyboard.readChar();

			if(pressedKey = String.backSpace())
			{
				do inputString.eraseLastChar();
			}
			else
			{
				do inputString.appendChar(pressedKey);
			}
		}

		// erase the newline
		do inputString.eraseLastChar();

		return inputString;
    }   

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its
     * integer value (until the first non numeric character).
     */
    function int readInt(String message) {
		var String inputString;
		let inputString = Keyboard.readLine(message);
		return inputString.intValue();
    }
}
